   1 C ZIIPPZARAS GYONGY ZSEBBEL ES GALLERRAL
   1 C PASSZEJA ZSAKKOTESU:Y:5/3
  1 C                                      E-H:70CM     U:59CM  
  1 C-----------------------+------------+-----------+--------+--------+--------+
  1 C Y5:3LUSI(FUZFA)  Y2:4LUSI(NYERS)        100        92        
  1 C Y3:3LUSI(FEKETE) Y4:4LUSI(FARMER)
  1 C-----------------------+------------+------------+------------+-------------+
C FONALFELHASZNALAS:
C  2:4NYERS            6*0.40= 
C  5:3FUST             6*0.32=   
C (4:4CSIK A PASSZEBAN)6*0.10= 
C  3:3FEKETE           6*0.10= 
C----------------------------------------------------
C     SUM.:            6*0.90=5.4  (0.9*505-516=455-465FT)
C------------------------------------------------------------------------------
  1 C   ZIPPES KARDIGAN.:
  1 C     FONAL: 455
  1 C VALLTOMES:   5
  1 C    VARRAS: 150
  1 C   VASALAS:  40
  1 C         ----------
  1 C     OSSZ.: 650
  1 C        E.:1050
  1 C        H.: 400
C------------------------------------------------------------------------------

C KT:NPK=0  PKT:NPK=-0.2
  10 NP4=12.6;   C:LEVALASZTAS
  12 NP2=11.0;   C:BEKEZDES
  14 NP3=13.0;   C:KORSLAG,ZSAKKOTESU NORVEG
  16 NP1=12.0;   C GYONGY
  17 NP6=12.0;   C KUKORICAS
  18 NP5=12.0;   C:1/1 GARBO

  40 START        
  50 YG:6/23456;
  52 WM=20;WMI=7
  54 C
  56 << S:D.I-DI./DI.-D.I;            Y:5/2;      NP4-4    VU V0 ML       S1 S2
C-------------GYONGY3SZV.-VEL----------------------------------------------
 60C IF #100=43  RS19=0           C E-H            21'
 61C IF #100=32  RS19=1           C U              16'
 62C IF #100=24  RS19=2           C 3DB.GALLER    
 63C IF #100=21  RS19=3           C 3DB.1/1 GARBO
C-------------GYONGY4SZV.-VEL(Y4:ALUL A KET NORVEG MINTA KOZT SZINES)------
 64C IF #100=18  RS19=4           C E-H            21'
 65C IF #100=10  RS19=5           C U              16'
 66C IF #100=4   RS19=2           C 3DB.GALLER    
 67C IF #100=2   RS19=3           C 3DB.1/1 GARBO
C----------------------------------------------------------------------------
C-------------KUKORICAS 3SZV.-VEL------------------------------------------
  68 IF #100=4   RS19=6           C E-H            21'
  69 IF #100=7   RS19=7           C U              16'
  70C IF #100=12  RS19=8           C 3DB.GALLER    
  71C IF #100=34   RS19=9           C 3DB.1/1 GARBO
  72 IF #100=13  RS19=14          C 1PAR PASSZE A TESTHEZ (CSIK NELKUL)
C-------------KUKORICAS 4SZV.-VEL(Y4:ALUL A KET NORVEG MINTA KOZT SZINES)--
  73C IF #100=12  RS19=10          C E-H            21'
  74C IF #100=18  RS19=11          C U              16'
  75C IF #100=4   RS19=8           C 3DB.GALLER    
  76C IF #100=4   RS19=9           C 3DB.1/1 GARBO
  77C IF #100=30  RS19=13          C 1 PAR PASSZE A TESTHEZ (CSIKKAL)
C----------------------------------------------------------------------------
C----------------------------------------------------------------------------
  78 C IF #100=35   RS19=15	     C 3DB.1/1 GARBO NYAK JACQ.	SZEGELLYEL (EZ A GARBO MINDEGYIKHEZ JO)
C-----------------'V' ALAKBAN RAVARRT PASSZE A TESTHEZ !!!!EZ MEG NINCS MEGCSINALVA!!!!!!!----------
  80 IF RS19=0  RS1=95  RS2=5  RS17=0     C E-H       3LAP  1001
  81 IF RS19=1  RS1=90  RS2=5  RS17=1     C U         4LAP   92
  82 IF RS19=2  RS1=15  RS2=1  RS17=2     C GALLER
  83 IF RS19=3  RS1=13  RS2=1  RS17=2     C 1/1 GARBO                  RS1=16 REGI
  84 IF RS19=4  RS1=82  RS2=18 RS17=0     C E-H       3LAP   87
  85 IF RS19=5  RS1=85  RS2=10 RS17=1     C U         4LAP   87
C----------------------------------------------------------------------------

C--------------'V' ALAKBAN RAVARRT PASSZE A TESTHEZ --------------------
  86 IF RS19=6  RS1=92         RS17=0     C E-H       3LAP        (CSIK NELKUL)
  87 IF RS19=7  RS1=88  RS2=3  RS17=1     C U         4LAP   92   (CSIK NELKUL)
  88 IF RS19=14 RS1=3   RS2=3  RS17=2     C 2DB. PASSZE A TESTHEZ (CSIK NELKUL)

  89 IF RS19=8  RS1=17         RS17=2     C GALLER
  90 IF RS19=9  RS1=15         RS17=2     C 1/1 GARBO                  RS1=18 REGI
  91 IF	RS19=15		       RS17=2	           C 1/1	GARBO NYAK JACQ. SZEGELLYEL

  93 IF RS19=10 RS1=92         RS17=0     C E-H       3LAP        (CSIKKAL)
  94 IF RS19=11 RS1=88  RS2=3  RS17=1     C U         4LAP   87   (CSIKKAL)
  95 IF RS19=13 RS1=3   RS2=3  RS17=2     C 2DB. PASSZE A TESTHEZ (CSIKKAL)
C------------------------------------------------------------------------------

  96 JA1=1002; PA:1:<JA1>;
  97 IF RS17=0 FA=1-179  PM:<*>6:3(FA**);    C**3 LAP E-H
  98 IF RS17=1 FA=1-135  PM:<*>7:4(FA);      C**4 LAP U       VAGY
  99 IF RS17=1 FA=1-134  PM:<*>8:4(FA*);     C**4 LAP U       VAGY
 100 IF RS17=2 FA=1-272  PM:<*>7:2FA;        C**3DB.GALLER VAGY 3DB.1/1 GARBO OSZTAS NELKUL
C------------------------------------------------------------------------------
 101 IF	RS19=15	GOTO 900		     C 1/1 GARBO NYAK JACQ. SZEGELLYEL

 150 >> S:D.I-DI./DI.-D.I/UVNDI.;                            ML       S1 S2 T2
 152 << S:DI.-D.I/UVND.I;             Y:3;	             	        ML       S1    T2
 154 >> S:R-R;		                      Y:6;	               VU 	   ML          S2
 156 << S:R-0/0-R;                    Y:6/0;            	 VU     ML       S1 S2
 158 >> S:R-R;                        Y:3;        NP2-2   VU    ML       S1
 160 Y:3;
 162 IF RS19=6  GOTO 140 C E-H
 164 IF RS19=10 GOTO 140 C E-H
 166 REP*2
 168 << S:R-0;		                                  NP3-3   VU  ML    S1
 170 >> S:0-R;		                                          VU  ML    S1
 172 REPEND

 174 IF RS19>5 GOTO 300 C***********$$$$$$$$$$$$$$$$$$$$$$ KUKORICAS $$$$$$$$$$$$$$$$$$$$$$$******************

C*****GYONGY  TEST-UJJA-NYAKAK (RS19=0-1-2-3-4-5)********************
 200 IF RS19=2 GOTO 208; C***GALLER***
 201 IF RS19=3 GOTO 208; C***GARBO***
 202 JA1=1000(1000-1002);
 204 Y:2/3;
 206 GOSUB 500-510*2 C**** ZSAKKOTESU NORVEG PASSZE*****

 208 Y:3;
 210 << S:R-0;		                           VU      S1
 212 JA1=1004;  C1004 .I.I.I.I.
 213 >> S:0-R/UVNI*;		                             S1    T2
 214 IF RS19=1 GOTO 242; C***UJJA***
 215 IF RS19=5 GOTO 242; C***UJJA***
 216 IF RS19=2 GOTO 242; C***GALLER***
 217 IF RS19=3 GOTO 242; C***GARBO***
 218 << S:U^N./I*-./UVN.;    NP1-1  Y:2;        T1 S1    T2
 220 >> S:R-R;               NP2-2       ML VU     S1
 222 << S:R-0;               NP3-3          VU     S1
 224 >> S:0-R;                              VU     S1
 226 JA1=1010(1010-1056) C**NORVEG**
 228 Y:3/2; GOSUB 500-510*4;
 230 Y:5/2; GOSUB 500-510*4;
 232 Y:3/2; GOSUB 500-510*4;
 234 Y:2;
 236 << S:R-0;		                            VU     S1
 238 JA1=1004;  C1004 .I.I.I.I.
 240 >> S:0-R/UVNI*;		                             S1   T2

 242 WM=28
 244 Y:2/5; IF RS19>3 Y:4/3;
 246 << S:U^N./I*-%./I*-.;  NP1-1         VU T1 S1 S2  C
 248 >> S:I*-%./I*-.;                     VU    S1 S2  C
 250 RBEG*RS2 C***KET NORVEG KOZTI GYONGY VAGY Y:2/5; VAGY Y:4/3;***
 252 GOSUB 600-610 C***GYONGY***
 254 REND
 256 << S:I*-%./I*-.;               VU          S1 S2  C
 258 >> S:I*-%./I*-./UVN.;                      S1 S2  T2 C

 260 WM=20; 
 262 << S:R-R;              Y:2; NP2-2   ML  VU    S1
 264 >> S:R-0;                   NP3-3       VU    S1
 268 JA1=1058(1058-1096)C**NORVEG**
 270 Y:3/2; GOSUB 500-510*2;
 272 Y:3/5; GOSUB 500-510*2;
 274 Y:2/5; GOSUB 500-510*2;
 276 Y:3/5; GOSUB 500-510*2;
 278 Y:3/2; GOSUB 500-510*2;
 280 Y:2;
 282 << S:R-0;		                           VU      S1
 284 JA1=1004;  C1004 .I.I.I.I.
 286 >> S:0-R/UVNI*;		                             S1  T2
 288 IF RS19=3 GOTO 750 C *** 1/1 GARBO ***
 290 WM=28
 291 << S:U^N./I*-%./I*-.;  NP1-1 Y:2/5;  VU    T1 S1 S2  C
 292 >> S:I*-%./I*-.;                     VU       S1 S2  C
 293 RBEG*RS1
 294 GOSUB 600-610 C***GYONGY***
 295 REND
 296 << S:I*-%./I*-.;            VU      S1 S2
 297 >> S:I*-%./I*-./UVN.;          ML   S1 S2 T2
 298 END
C**********************************************************************************

C*****KUKORICAS  TEST-UJJA-NYAKAK (RS19=6-7-8-9-10-11-13-14)*****************************
 300 << S:R-0;		                      Y:3;   VU    S1
 302 JA1=1004;  C1004 .I.I.I.I.
 303 >> S:0-R/UVNI*;		                             S1    T2
 304 IF RS19=10 GOTO 380; C                                                   ***TEST ***
 305 IF RS19=6  GOTO 380; C                                                   ***TEST ***
 306 IF RS19=7  GOTO 336; C                                                   ***UJJA***
 307 IF RS19=11 GOTO 336; C                                                   ***UJJA***
 308 << S:U^N./I*-./UVN.;    NP1-1  Y:2;        T1 S1    T2
 310 >> S:R-R;               NP2-2       ML VU     S1
 312 << S:R-0;               NP3-3          VU      S1
 314 >> S:0-R;                              VU     S1
 316 JA1=1010(1010-1056) C**NORVEG**
 318 Y:3/2; GOSUB 500-510*4;
 320 Y:5/2; GOSUB 500-510*4;
 322 Y:3/2; GOSUB 500-510*4;
 324 Y:2;
 326 << S:R-0;		                            VU     S1
 328 JA1=1004;  C1004 .I.I.I.I.
 330 >> S:0-R/UVNI*;		                             S1   T2
 332 IF RS19=9 GOTO 750 C                                                  *** 1/1 GARBO ***
 334 IF RS19=8 GOTO 380 C                                                  ***GALLER***
 336 WM=28

 337 Y:5/2/2/5;            C       (CSIK NELKUL)
 338 IF RS19=11 Y:3/4/4/3; C UJJA  (CSIKOS)
 339 IF RS19=13 Y:3/4/4/3; C PASSZE (CSIKOS)

 340 << S:U^N./I*-./I*-%.;  NP6-6         VU T1 S1 S2  C
 342 >> S:I*-%./I*-.;                     VU    S1 S2  C
 344 RBEG*RS2 C***KET NORVEG KOZTI KUKORICA VAGY Y:5/2/2/5; VAGY Y:3/4/4/3;***
 346 GOSUB 700-710 C***KUKORICA***
 348 REND
 350 << S:I*-./I*-%.;               VU          S1 S2  C
 352 >> S:I*-%./I*-./UVN.;                      S1 S2  T2 C
 354 WM=20; 
 356 << S:R-R;              Y:2; NP2-2   ML  VU    S1
 358 >> S:R-0;                   NP3-3       VU    S1
 360 JA1=1058(1058-1096)C**NORVEG**
 362 Y:3/2; GOSUB 500-510*2;
 364 Y:3/5; GOSUB 500-510*2;
 366 Y:2/5; GOSUB 500-510*2;
 368 Y:3/5; GOSUB 500-510*2;
 370 Y:3/2; GOSUB 500-510*2;
 372 Y:2;
 374 << S:R-0;		                           VU      S1
 376 JA1=1004;  C1004 .I.I.I.I.
 378 >> S:0-R/UVNI*;		                             S1  T2
 380 WM=28
 382 Y:5/2/2/5;
 384 << S:U^N./I*-./I*-%.;  NP6-6         VU T1 S1 S2  C
 386 >> S:I*-%./I*-.;                     VU    S1 S2  C
 388 RBEG*RS1
 390 GOSUB 700-710 C***KUKORICA***
 392 REND
 394 << S:I*-./I*-%.;            VU      S1 S2
 396 >> S:I*-./I*-./UVN.;           ML   S1 S2 T2
 398 END
C**********************************************************************************


 500 C**************ZSAKKOTESU NORVEG MINTA***********************
 502 WM=20
 504 NP3-3
 506 << S:<1+>I-.*/.*-I;           VU          S1 S2
 508 >> S:<1+>I*-./.-I*;           VU          S1 S2
 510 C************************************************************

 600 C*****GYONGY********Y:2/5; VAGY Y:4/3;***********************
 602 WM=28
 604 NP1-1
 606 << S:I*-%./I*-.;          VU          S1 S2  C
 608 >> S:I*-%./I*-.;          VU          S1 S2  C
 610 C************************************************************

 700 C*****DUPLA GYONGY (KUKORICAS) Y:3/4/4/3; VAGY Y:5/2/2/5;****
 702 WM=28
 704 NP6-6
 706 << S:I*-./I*-%.;          VU          S1 S2  C
 708 >> S:I*-%./I*-.;          VU          S1 S2  C
 710 C************************************************************

 750 C**********1/1 GARBO NMYAK************************************
 752 << S:U^N./I*-./I*-.;   Y:2/5;   NP5-5   VU T1 S1 S2
 754 RBEG*RS1
 756 >> S:I*-./I*-.;                    VU         S1 S2
 758 <<                                 VU         S1 S2
 760 REND
 762 >> S:I*-./I*-./UVN.;                          S1 S2 T2
 764 END
 766 C***********************************************

C******************* 1/1 GARBO**************
 900 WM=18;WMI=7
 902 >>	S:D.I-DI./UVNDI.;		   Y:5;	   ML			            S1	 T2
 904 <<	S:U^ND.I/DI.-D.I;			  Y:5;			 ML	         		T1 S1
 906 >>	S:DI.-D.I;		          Y:5;    ML		VU  VR1	     S1
 908 <<	S:DI.-0/UVND.I;		     Y:5;    ML		VKA>3	       S1    T2
 910 >>	S:DI.-D.I;		          Y:6;    ML		   V0 VU     S1
 912 <<	S:DI.-0/0-R;		        Y:6/0;  ML		      VU     S1 S2	        
 914 >>	S:DI.-D.I/DI.-0;			   Y:2/5;	 ML NP5-5	 VU	    S1 S2
 916 <<	S:0-D.I/DI.-D.I;				                    VU     S1 S2
 918 >>	S:DI.-D.I;					                         VU     S1 S2
 920 REP*1
 922 <<	S:DI.-D.I;				                          VU	    S1 S2
 924 >>						                                   VU	    S1 S2
 926 REPEND
 928 <<	S:DI.-D.I/UVND.I;           NP1-1  Y:2;	       S1	   T2
 930 >>	S:R-R;		                    NP2-2	ML    VU     S1
 932 JA1=1100(1100-1118)
 934 Y:3/2; GOSUB 500-510;
 936 Y:3/5; GOSUB 500-510;
 938 Y:2/5; GOSUB 500-510;
 940 Y:3/5; GOSUB 500-510;
 942 Y:3/2; GOSUB 500-510;
 944 <<	S:0-R/R-0;			       Y:5/2;	       VU    S1 S2
 946 JA1=1004;	C1004 .I.I.I.I.
 948 >>	S:0-R/R-0/UVNI*;					                   S1 S2 T2
 950 <<	S:U^N./I*-./I*-.;   Y:2/5;   NP5-5	  T1 S1 S2
 952 REP*15
 954 >>	S:I*-./I*-.;			                   VU	   S1 S2
 956 <<					                              VU	   S1 S2
 958 REPEND
 960 >>	S:I*-./I*-./UVN.;	           NP4-4	     S1 S2 T2
 962 END


 999 <> W0 S0

1000 ..II
1002 ..II

1004 .I.I


1010 ....
1012 .I.I
1014 .III
1016 .III
1018 II.I
1020 I...
1022 I...
1024 I...

1026 ..I.
1028 .III
1030 .III
1032 .I.I
1034 .I.I
1036 II.I
1038 II.I
1040 I...

1042 ..I.
1044 ..I.
1046 ..I.
1048 .III
1050 II.I
1052 II.I
1054 .I.I
1056 ....


CY:.2
C  I3
1058 ......
1060 ...II.
1062 ..IIII
1064 .IIIII
CY:.5
C  I3
1066 II...I
1068 I.....
1070 II...I
1072 I.....
CY:.5
C  I2
1074 ...I..
1076 ..IIII
1078 .IIII.
1080 ...I..
CY:.5
C  I3
1082 I.....
1084 II...I
1086 I.....
1088 II...I
CY:.2
C  I3
1090 .IIIII
1092 ..IIII
1094 ...II.
1096 ......

1100 ..III.
1102 .IIIII
1104 II...I
1106 I.....
1108 ...II.
1110 ..II..
1112 I.....
1114 II...I
1116 .IIIII
1118 ..III.
